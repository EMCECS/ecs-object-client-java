/*
 * Copyright (c) 2015, EMC Corporation.
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *
 * + Redistributions of source code must retain the above copyright notice,
 *   this list of conditions and the following disclaimer.
 * + Redistributions in binary form must reproduce the above copyright
 *   notice, this list of conditions and the following disclaimer in the
 *   documentation and/or other materials provided with the distribution.
 * + The name of EMC Corporation may not be used to endorse or promote
 *   products derived from this software without specific prior written
 *   permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
 * TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS
 * BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */
//use new plugins method
plugins {
    id 'net.saliman.cobertura' version '3.0.0' apply false
    id 'org.ajoberstar.git-publish' version '3.0.0' apply false
    id 'nebula.release' version '15.2.0' apply false
}

description = 'EMC Object Client for Java - provides REST access to object data on EMC platforms using the Atmos and S3 APIs.'

ext.githubProjectName = 'ecs-object-client-java'

allprojects {
    group 'com.emc.ecs'

    ext.githubProjectName = project.name
    ext.scmUrl = "https://github.com/EMCECS/${githubProjectName}"
    ext.scmConnection = "scm:git@github.com:EMCECS/${githubProjectName}.git"
    ext.githubUrl = "https://github.com/EMCECS/${githubProjectName}.git"
    ext.aggregatedDocsDir = "$buildDir/aggregatedDocs"

    ext.licenseName = 'The BSD 3-Clause License'
    ext.licenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'

    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'net.saliman.cobertura'
    apply plugin: 'distribution'
    apply plugin: 'signing'
    apply plugin: 'maven'

    defaultTasks 'distZip'


    configurations {
        jars.extendsFrom(signatures)
        export // specifies additional artifacts in the root distribution
        tools // specifies tool artifacts for the root distribution (in the tools/ directory)
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':uploadJars')) {
        if (!rootProject.hasProperty('signingPass')) {
            rootProject.ext.signingPass = new String(System.console().readPassword('\nSigning key passphrase: '))
            rootProject.ext.sonatypePass = new String(System.console().readPassword('\nSonatype password: '))
        }
        ext.'signing.keyId' = signingKeyId
        ext.'signing.secretKeyRingFile' = signingSecretKeyRingFile
        ext.'signing.password' = rootProject.ext.signingPass
        uploadJars.repositories.mavenDeployer.repository.authentication.password = rootProject.ext.sonatypePass
    }
    if (taskGraph.hasTask(':publishGhPages')) {
        githubPages.credentials.password = new String(System.console().readPassword('\nGithub password: '))
    }
    if (taskGraph.hasTask(':release')) {
        def gitPassword = new String(System.console().readPassword('\nOrigin password: '))
        System.setProperty('org.ajoberstar.grgit.auth.username', gitUsername)
        System.setProperty('org.ajoberstar.grgit.auth.password', gitPassword)
    }

}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    if (project.hasProperty('java8Lib')) {
        options.fork = true
        options.bootClasspath = new File(java6Lib).listFiles(
                [accept: { d, f -> f ==~ /.*\.jar/ }] as FilenameFilter
        ).join(File.pathSeparator)
    }
}


def projectPom = {
    project {
        name project.name
        description project.description
        url 'https://community.emc.com/community/products/vipr#developer'

        scm {
            url scmUrl
            connection scmConnection
            developerConnection scmConnection
        }

        licenses {
            license {
                name licenseName
                url licenseUrl
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'EMCECS'
                name 'Dell EMC ECS'
            }
        }
    }
}

task writePom {
    ext.pomFile = file("$buildDir/pom.xml")
    outputs.file pomFile
    doLast {
        pom(projectPom).writeTo pomFile
    }
}

jar {
    doFirst {
        manifest {
            attributes 'Implementation-Version': project.version,
                    'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        }
    }
    into("META-INF/maven/$project.group/$project.name") {
        from writePom
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${docsDir}/javadoc"
}
javadocJar.dependsOn javadoc

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    jars jar
    jars javadocJar
    jars sourcesJar
}

distributions {
    main {
        contents {
            from configurations.jars.artifacts.files
            from configurations.export.artifacts.files
            from { subprojects.configurations.export.artifacts.files }
            into('tools') {
                from configurations.tools.artifacts.files
                from { subprojects.configurations.tools.artifacts.files }
            }
            from('.') {
                include '*.txt'
            }
            into('3rd-party-licenses') {
                from '3rd-party-licenses'
            }
            into('lib') {
                from configurations.runtime
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask(':uploadJars') }
    sign configurations.jars
}

uploadJars {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: '')
            }

            pom projectPom
        }
    }
}

// "<<" was deprecated in gradle 5.X
task aggregateDocs {
    doLast {
        def docSubDir = ''
        if (project.hasProperty('release.stage') && project.ext['release.stage'] == 'final') {
            copy {
                from docsDir
                into "${rootProject.aggregatedDocsDir}/latest${docSubDir}"
            }
        }
        copy {
            from docsDir
            into "${rootProject.aggregatedDocsDir}/${project.version}${docSubDir}"
        }
    }
}
aggregateDocs.dependsOn javadoc
aggregateDocs.dependsOn { subprojects.collect { it.tasks['aggregateDocs'] } }


apply plugin: 'base'
// org.ajoberstar.github-pages => git-publish
apply plugin: 'org.ajoberstar.git-publish'
// org.ajoberstar.release-opinion => nebula.release
apply plugin: 'nebula.release'

//migrating from org.ajoberstar.github-pages to org.ajoberstar.release-opinion
//https://github.com/ajoberstar/gradle-git-publish#migrating-from-orgajoberstargithub-pages
gitPublish {
    repoUri = githubUrl
    branch = 'gh-pages'
    contents {
        from aggregatedDocsDir
    }
    preserve { include '**/*' }
}
gitPublishPush.dependsOn aggregateDocs, test

tasks.release.dependsOn test, uploadJars, gitPublishPush, distZip

clean {
    delete aggregatedDocsDir
}

dependencies {
    compile 'log4j:log4j:1.2.17',
            'com.emc.ecs:smart-client:2.2.1',
            'com.emc.ecs:object-transform:1.1.0',
            'commons-codec:commons-codec:1.10',
            'org.jdom:jdom2:2.0.6',
            'org.slf4j:slf4j-api:1.7.5'
    runtime 'org.slf4j:slf4j-log4j12:1.7.5'
    testCompile 'junit:junit:4.12'
}
