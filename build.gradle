/*
 * Copyright (c) 2015-2020, EMC Corporation.
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *
 * + Redistributions of source code must retain the above copyright notice,
 *   this list of conditions and the following disclaimer.
 * + Redistributions in binary form must reproduce the above copyright
 *   notice, this list of conditions and the following disclaimer in the
 *   documentation and/or other materials provided with the distribution.
 * + The name of EMC Corporation may not be used to endorse or promote
 *   products derived from this software without specific prior written
 *   permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
 * TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 * PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS
 * BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */
import com.github.jk1.license.render.InventoryHtmlReportRenderer

plugins {
    id 'idea'
    id 'eclipse'
    id 'java'
    id 'net.saliman.cobertura' version '4.0.0'
    id 'com.github.jk1.dependency-license-report' version '1.17'
    id 'distribution'
    id 'signing'
    id 'maven'
    id 'org.ajoberstar.git-publish' version '3.0.1'
    id 'nebula.release' version '15.3.1'
}

group 'com.emc.ecs'
description = 'EMC Object Client for Java - provides REST access to object data on EMC platforms using the Atmos and S3 APIs.'

// name of the github project repository
ext.githubProjectName = 'ecs-object-client-java'
// URL to github project
ext.githubProjectUrl = "https://github.com/EMCECS/${githubProjectName}"
// git remote scm address
ext.githubScmUrl = "scm:git@github.com:EMCECS/${githubProjectName}.git"
// git remote https address
ext.githubRemoteUrl = "https://github.com/EMCECS/${githubProjectName}.git"
// license info
ext.licenseName = 'The BSD 3-Clause License'
ext.licenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'

defaultTasks 'distZip'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'com.emc.ecs:smart-client-ecs:3.0.5'
    implementation 'com.sun.jersey.contribs:jersey-apache-client4:1.19.4'
    // NOTE: Jackson 2.13 dropped support for JAX-RS 1.x, and we use Jersey client 1.x, so we are stuck on Jackson 1.12.x
    // ref: https://github.com/FasterXML/jackson-jaxrs-providers/issues/90#issuecomment-1081368194
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.12.7'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.12.7'
    implementation('com.emc.ecs:object-transform:1.1.0') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    implementation 'commons-codec:commons-codec:1.15'
    implementation('org.dom4j:dom4j:2.1.3') {
        // workaround for jdom2 bug (https://github.com/dom4j/dom4j/issues/99)
        // NOTE: a component metadata rule will not solve the problem for library consumers - this is the only way
        exclude module: 'pull-parser'
        exclude module: 'jaxen'
        exclude module: 'xpp3'
        exclude module: 'xsdlib'
        exclude module: 'stax-api'
        exclude module: 'jaxb-api'
    }
    implementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testRuntimeOnly 'org.slf4j:jcl-over-slf4j:1.7.36'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
}

allprojects {
    configurations {
        tools // specifies tool artifacts for the distribution (placed in the tools/ directory)
    }
}

configurations {
    jars.extendsFrom(signatures)
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.8

def projectPom = {
    project {
        name project.name
        description project.description
        url githubProjectUrl

        scm {
            url githubProjectUrl
            connection githubScmUrl
            developerConnection githubScmUrl
        }

        licenses {
            license {
                name licenseName
                url licenseUrl
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'EMCECS'
                name 'Dell EMC ECS'
            }
        }
    }
}

task writePom {
    ext.pomFile = file("$buildDir/pom.xml")
    outputs.file pomFile
    doLast {
        pom(projectPom).writeTo pomFile
    }
}

jar {
    doFirst {
        manifest {
            attributes 'Implementation-Version': project.version,
                    'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        }
    }
    into("META-INF/maven/$project.group/$project.name") {
        from writePom
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from "${docsDir}/javadoc"
}
tasks.javadocJar.dependsOn javadoc

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    jars jar
    jars javadocJar
    jars sourcesJar
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

distributions {
    main {
        contents {
            from configurations.jars.artifacts.files
            into('tools') {
                from { allprojects.configurations.tools.artifacts.files }
            }
            from('.') {
                include '*.txt'
            }
            into('3rd-party-licenses') {
                from generateLicenseReport
            }
            into('lib') {
                from configurations.runtimeClasspath
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask(':uploadJars') }
    sign configurations.jars
}

uploadJars {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: '', password: '')
            }

            pom projectPom
        }
    }
}

ext.aggregatedDocsDir = "$buildDir/aggregatedDocs"
task aggregateDocs {
    doLast {
        if (project.hasProperty('release.stage') && project.ext['release.stage'] == 'final') {
            copy {
                from docsDir
                into "${aggregatedDocsDir}/latest"
            }
        }
        copy {
            from docsDir
            into "${aggregatedDocsDir}/${project.version}"
        }
    }
}
tasks.aggregateDocs.dependsOn javadoc

gitPublish {
    repoUri = githubRemoteUrl
    branch = 'gh-pages'
    contents {
        from aggregatedDocsDir
    }
    preserve { include '**/*' }
}
tasks.gitPublishPush.dependsOn aggregateDocs

tasks.release.dependsOn test, uploadJars, gitPublishPush, distZip

clean {
    delete aggregatedDocsDir
}

// allow typing in credentials
// note: this only works when run without the Gradle daemon (--no-daemon)
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':uploadJars')) {
        if (!rootProject.hasProperty('signingSecretKeyRingFile'))
            rootProject.ext.signingSecretKeyRingFile = new String(System.console().readLine('\nSecret key ring file: '))
        if (!rootProject.hasProperty('signingKeyId'))
            rootProject.ext.signingKeyId = new String(System.console().readLine('\nSigning key id: '))
        if (!rootProject.hasProperty('signingPass'))
            rootProject.ext.signingPass = new String(System.console().readPassword('\nSigning key passphrase: '))
        if (!rootProject.hasProperty('sonatypeUser'))
            rootProject.ext.sonatypeUser = new String(System.console().readLine('\nSonatype username: '))
        if (!rootProject.hasProperty('sonatypePass'))
            rootProject.ext.sonatypePass = new String(System.console().readPassword('\nSonatype password: '))
        ext.'signing.keyId' = rootProject.ext.signingKeyId
        ext.'signing.secretKeyRingFile' = rootProject.ext.signingSecretKeyRingFile
        ext.'signing.password' = rootProject.ext.signingPass
        uploadJars.repositories.mavenDeployer.repository.authentication.userName = rootProject.ext.sonatypeUser
        uploadJars.repositories.mavenDeployer.repository.authentication.password = rootProject.ext.sonatypePass
    }
    if (taskGraph.hasTask(':gitPublishPush') || taskGraph.hasTask(':release')) {
        if (!rootProject.hasProperty('gitUsername'))
            rootProject.ext.gitUsername = new String(System.console().readLine('\nGit username: '))
        if (!rootProject.hasProperty('gitPassword'))
            rootProject.ext.gitPassword = new String(System.console().readPassword('\nGit password: '))
        System.setProperty('org.ajoberstar.grgit.auth.username', rootProject.ext.gitUsername)
        System.setProperty('org.ajoberstar.grgit.auth.password', rootProject.ext.gitPassword)
    }
}
